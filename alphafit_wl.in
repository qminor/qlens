# The correct lens model for the simulated data used here is as follows:
# alpha (b=5, s=0, q=0.7, theta=90, center=(0.9,0.3)
# external shear (gamma=0.1, theta=40 degrees)
# Source point: (0.5, 0.7)
#
# In this script we show how to infer these parameters by fitting a lens model
# to the simulated data using chi-square minimization.

lens clear              # Just in case we were doing something else before this
imgdata clear           # Just in case we already had image data loaded before this
wldata clear            # Just in case we already had weak lensing data loaded before this

fit label alphafit      # This labels the files that will be produced by the lens fitting
fit method simplex      # This is the default fit method (downhill simplex method), which minimizes the chi-square function
chisqlog off  	         # if turned on, this saves all the chi-square function evaluations in file "<label>.log"
sci_notation off        # This is just up to your personal preference
central_image off       # The image data does not include a central image, so this tells QLens not to search for one.
wldata read alpha_wl.dat    # Loads in the weak lensing data
wldata                      # Shows the image data so you can see what it looks like

# Here are some tips for deciding your initial guess for the lens parameters:
# Use the scale of the images to guess the Einstein radius.
# Always fix alpha=1 and s=0 (singular isothermal ellipsoid model). These parameters usually cannot be
#    constrained well unless we have time delays or multiple source points.
# The center of the lens can often be guessed at by averaging the image positions (or using the center of the
#    lens galaxy in the image if it is visible).
# It may only be possible to constrain both the external shear and axis ratio q if image fluxes or time delays
#    can be used in addition to the image positions. If you do model the external shear, guess a small
#    but nonzero value for both gamma and the angle, as we do here.

fit lens alpha 4.5 1 0 0.8 30 0.7 0.3 shear=0.02 10 # Now we put our initial guess for the lens parameters
1 0 0 1 1 1 1 1 1                                   #vary flags; here we vary everything except for alpha and s (core size)

chisqtol 1e-3   # Sets the convergence criterion for finding the minimum chi-square. Bigger chisqtol --> less accuracy, but runs faster

# By default, the chi-square function is set to the source-plane chi-square (see B.4.6 of the Saas Fee Lectures by Kochanek (2004))
#fit run            # for the first run, you might want to use the source plane chi-square to find the right region of
                    #   parameter space (which is faster and sometimes less prone to getting stuck in a rut, but not always accurate)
#fit use_bestfit    # adopt the best fit parameters so they can be used as initial parameters for the next run

nrepeat 2           # this tells it to minimize the chi-square again after converging to a best-fit point, just to ensure we are
                    # really at a minimum (recommended)
						  
simplex_temp0 = 100000

fit run
fit use_bestfit
fit plotshear       # plot the reduced shear generated by our best-fit model at the positions of the data images
pause

# Now let's see how our fit does with the strong lensing data included, and reoptimize!
imgdata read alphafit.dat
fit chisq
fit sourcept auto
fit plotimg
pause 

simplex_temp0 = 0   # we should be close enough to the solution that we don't need to use annealing, hopefully
imgplane_chisq on
fit run
fit use_bestfit

fit plotimg
fit plotshear
